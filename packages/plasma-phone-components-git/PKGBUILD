# Maintainer: Bhushan Shah <bhush94 at gmail dot com>
pkgname=plasma-phone-components-git
pkgver=r546.b6bbc20
pkgrel=1
pkgdesc="Modules providing phone functionality for Plasma"
arch=('i686' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('plasma-workspace' 'kpeople' 'telepathy-qt5')
makedepends=('git' 'cmake')
provides=('plasma-phone-components')
source=('git://anongit.kde.org/plasma-phone-components.git')
install=plasma-phone-components-git.install
md5sums=('SKIP')

_gitname="plasma-phone-components"

pkgver() {
  cd plasma-phone-components
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-phone-components \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
