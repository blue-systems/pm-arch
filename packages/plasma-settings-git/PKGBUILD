# Maintainer: Bhushan Shah <bhush94 at gmail dot com>
pkgname=plasma-settings-git
pkgver=r534.15287cd
pkgrel=1
pkgdesc="Settings application for Mobile Devices running Plasma"
arch=('i686' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('plasma-framework' 'kdelibs4support')
makedepends=('git' 'cmake')
provides=('plasma-settings')
source=('git://anongit.kde.org/plasma-settings.git')
md5sums=('SKIP')

_gitname="plasma-settings"

pkgver() {
  cd plasma-settings
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-settings \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
