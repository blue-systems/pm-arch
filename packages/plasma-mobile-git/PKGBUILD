# Maintainer: Bhushan Shah <bhush94 at gmail dot com>
pkgname=plasma-mobile-git
pkgver=r5726.978b03c
pkgrel=1
pkgdesc="Plasma Active"
arch=('i686' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('plasma-workspace')
makedepends=('git' 'cmake' 'baloo' 'extra-cmake-modules' 'kdoctools')
provides=('plasma-mobile')
source=('git://anongit.kde.org/plasma-mobile.git')
install=plasma-mobile-git.install
md5sums=('SKIP')

_gitname="plasma-mobile"

pkgver() {
  cd plasma-mobile
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-mobile \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
